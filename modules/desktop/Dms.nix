{ inputs, ... }:

{
  flake.nixosModules.Dms =
    {
      pkgs,
      lib,
      config,
      ...
    }:
    let
      theme = config.vayume.theme;

      registryPlugins = pkgs.callPackage "${inputs.dms-plugin-registry}/nix/default.nix" { };

      audioIsPlayingScript = pkgs.writeShellScript "vayume-audio-is-playing" ''
        set -euo pipefail
        ${pkgs.pipewire}/bin/pw-dump | ${pkgs.jq}/bin/jq -e --arg cava cava '
          (map(select(.type=="PipeWire:Interface:Metadata" and .props["metadata.name"]=="default"))
            | .[0].metadata[]? | select(.key=="default.audio.sink") | .value.name) as $sinkname
          | . as $all
          | ($all | map(select(.type=="PipeWire:Interface:Node" and .info.props["node.name"]==$sinkname)) | .[0].id) as $sinkid
          | ($all | map(select(.type=="PipeWire:Interface:Node"))
              | map({(.id|tostring): (.info.props["application.name"] // .info.props["node.name"] // "")}) | add) as $nodenames
          | ($all | map(select(.type=="PipeWire:Interface:Link" and .info.state=="active" and .info.props["link.input.node"]==$sinkid))) as $activelinks
          | ($activelinks | map($nodenames[(.info.props["link.output.node"]|tostring)] // "")) as $names
          | ($names | any(. != $cava))
        ' > /dev/null
      '';

      assertPatched = file: needle: ''
        grep -qF ${lib.escapeShellArg needle} "${file}" || {
          echo "patch verification failed: ${lib.escapeShellArg needle} not found in ${file} (upstream source likely changed - update the patch in dms.nix)" >&2
          exit 1
        }
      '';

      assertPatchedLine = file: line: needle: ''
        sed -n '${toString line}p' "${file}" | grep -qF ${lib.escapeShellArg needle} || {
          echo "patch verification failed: line ${toString line} of ${file} doesn't say ${lib.escapeShellArg needle} (upstream source likely changed - update the patch in dms.nix)" >&2
          exit 1
        }
      '';

      mkPatchedPlugin =
        name: src: patchScript:
        pkgs.runCommand "dms-plugin-${name}-patched" { } ''
          cp -r ${src} $out
          chmod -R u+w $out
          ${patchScript}
        '';

      nixMonitorPatched = mkPatchedPlugin "nixMonitor" registryPlugins.nixMonitor ''
        sed -i '110s/Theme\.primary$/Theme.widgetIconColor/' $out/NixMonitor.qml
        ${assertPatchedLine "$out/NixMonitor.qml" 110 "Theme.widgetIconColor"}
      '';

      dankAsusControlCenterPatched =
        mkPatchedPlugin "dankAsusControlCenter" registryPlugins.dankAsusControlCenter
          ''
            substituteInPlace $out/DankAsusControlCenter.qml \
              --replace-quiet "size: root.showBatteryIcon ? 18 : Theme.iconSize * 0.85" "size: root.showBatteryIcon ? 18 : root.iconSize"
            sed -i '521s/spacing: 4$/spacing: Theme.spacingXS/' $out/DankAsusControlCenter.qml
            ${assertPatched "$out/DankAsusControlCenter.qml" "size: root.showBatteryIcon ? 18 : root.iconSize"}
            ${assertPatchedLine "$out/DankAsusControlCenter.qml" 521 "Theme.spacingXS"}
          '';

      cavaVisualizerPatched =
        let
          watchdogProps = lib.concatStringsSep "\n" [
            "readonly property int maxRetries: 3"
            ""
            "    property bool playbackActive: false"
            ""
            "    Timer {"
            "        interval: 2000"
            "        running: true"
            "        repeat: true"
            "        triggeredOnStart: true"
            "        onTriggered: playbackCheck.running = true"
            "    }"
            ""
            "    Process {"
            "        id: playbackCheck"
            ("        command: [\"" + "${audioIsPlayingScript}" + "\"]")
            "        running: false"
            "        onExited: exitCode => {"
            "            root.playbackActive = exitCode === 0"
            "            if (root.playbackActive) {"
            "                if (!configWriter.running && !cavaProcess.running)"
            "                    configWriter.running = true"
            "            } else if (cavaProcess.running) {"
            "                cavaProcess.running = false"
            "            }"
            "        }"
            "    }"
          ];

          oldRetryGuard = "if (!running && !configWriter.running) {";
          newRetryGuard = "if (!running && !configWriter.running && root.playbackActive) {";
        in
        mkPatchedPlugin "cavaVisualizer" registryPlugins.cavaVisualizer ''
          substituteInPlace $out/CavaVisualizerTab.qml \
            --replace-quiet ${lib.escapeShellArg "readonly property int maxRetries: 3"} ${lib.escapeShellArg watchdogProps}
          ${assertPatched "$out/CavaVisualizerTab.qml" "playbackActive"}

          substituteInPlace $out/CavaVisualizerTab.qml \
            --replace-quiet \
              '"[general]\n" +' \
              '"[input]\n" + "method = pipewire\n" + "source = auto\n" + "\n" + "[general]\n" +'
          ${assertPatched "$out/CavaVisualizerTab.qml" "source = auto"}

          substituteInPlace $out/CavaVisualizerTab.qml \
            --replace-quiet ${lib.escapeShellArg oldRetryGuard} ${lib.escapeShellArg newRetryGuard}
          ${assertPatched "$out/CavaVisualizerTab.qml" "playbackActive) {"}
        '';

      dmsShellPatched =
        let
          origDmsShell = inputs.dms.packages.${pkgs.system}.dms-shell;

          inputReplacement = lib.concatStringsSep "\n" [
            "[input]"
            "method=pipewire"
            "source=auto"
            ""
            "[general]"
          ];

          watchdogBlock = lib.concatStringsSep "\n" [
            "property bool playbackActive: false"
            ""
            "    Timer {"
            "        interval: 2000"
            "        running: root.refCount > 0 && root.cavaAvailable"
            "        repeat: true"
            "        triggeredOnStart: true"
            "        onTriggered: playbackCheck.running = true"
            "    }"
            ""
            "    Process {"
            "        id: playbackCheck"
            ("        command: [\"" + "${audioIsPlayingScript}" + "\"]")
            "        running: false"
            "        onExited: exitCode => {"
            "            root.playbackActive = exitCode === 0;"
            "        }"
            "    }"
            ""
            "    Process {"
            "        id: cavaProcess"
          ];

          oldRunning = "running: root.cavaAvailable && root.refCount > 0";
          newRunning = "running: root.cavaAvailable && root.refCount > 0 && root.playbackActive";
        in
        pkgs.runCommand "${origDmsShell.name}-cava-patched" {
          meta = (origDmsShell.meta or { }) // {
            mainProgram = "dms";
          };
        } ''
          cp -r ${origDmsShell} $out
          chmod -R u+w $out

          substituteInPlace $out/share/quickshell/dms/Services/CavaService.qml \
            --replace-quiet ${lib.escapeShellArg "[general]"} ${lib.escapeShellArg inputReplacement}
          ${assertPatched "$out/share/quickshell/dms/Services/CavaService.qml" "source=auto"}

          substituteInPlace $out/share/quickshell/dms/Services/CavaService.qml \
            --replace-quiet ${lib.escapeShellArg "    Process {\n        id: cavaProcess"} ${lib.escapeShellArg watchdogBlock}
          substituteInPlace $out/share/quickshell/dms/Services/CavaService.qml \
            --replace-quiet ${lib.escapeShellArg oldRunning} ${lib.escapeShellArg newRunning}
          ${assertPatched "$out/share/quickshell/dms/Services/CavaService.qml" "playbackActive"}

          substituteInPlace $out/share/quickshell/dms/shell.qml \
            --replace-quiet \
              ${lib.escapeShellArg "active: SettingsData.blurredWallpaperLayer && CompositorService.isNiri"} \
              ${lib.escapeShellArg "active: SettingsData.blurredWallpaperLayer && (CompositorService.isNiri || CompositorService.isHyprland)"}
          ${assertPatched "$out/share/quickshell/dms/shell.qml" "CompositorService.isHyprland"}

          substituteInPlace $out/bin/dms \
            --replace-quiet "${origDmsShell}/share/quickshell/dms" "$out/share/quickshell/dms"
          if grep -qF ${lib.escapeShellArg "${origDmsShell}/share/quickshell/dms"} "$out/bin/dms"; then
            echo "patch verification failed: bin/dms still references the original share/quickshell/dms path (upstream wrapper script format likely changed - update the patch in dms.nix)" >&2
            exit 1
          fi
        '';

      materialOSIcons = pkgs.stdenvNoCC.mkDerivation {
        pname = "materialos-icon-theme";
        version = "unstable-2026-08-27";
        src = pkgs.fetchFromGitHub {
          owner = "materialos";
          repo = "Linux-Icon-Pack";
          rev = "7ff36403cb38c0f5b7231df717a2efd373c94b6c";
          hash = "sha256-iLhaCdH1RlElMoWvmArTcX+VUubcyIX5k9vHBV9rU9Q=";
        };
        installPhase = ''
          mkdir -p $out/share/icons
          cp -r Icons/MaterialOS $out/share/icons/MaterialOS
        '';
      };

      vayumeHomeByUser = lib.concatMapStringsSep "\n" (
        name: ''"${name}") echo "${config.users.users.${name}.home}" ;;''
      ) (builtins.attrNames config.vayume.users);

      vayumeRebuildScript = pkgs.writeShellScript "vayume-rebuild" ''
        homeDir="$(case "''${SUDO_USER:-$USER}" in
        ${vayumeHomeByUser}
          *) echo "$HOME" ;;
        esac)"
        flakeDir=""
        for d in "$homeDir/vayume" "$homeDir/dotfiles" "$homeDir/.dotfiles" /etc/nixos; do
          [ -f "$d/flake.nix" ] && flakeDir="$d" && break
        done
        if [ -z "$flakeDir" ]; then
          echo "vayume flake not found (checked $homeDir/vayume, $homeDir/dotfiles, $homeDir/.dotfiles, /etc/nixos) - edit rebuildCommand in dms.nix if it lives elsewhere"
          exit 1
        fi
        ${pkgs.git}/bin/git config --global --add safe.directory "$flakeDir"
        exec nixos-rebuild switch --flake "path:$flakeDir#${config.networking.hostName}"
      '';

      vayumeGcScript = pkgs.writeShellScript "vayume-gc" "exec nix-collect-garbage -d";
    in
    {
      services.accounts-daemon.enable = true;

      security.sudo.extraRules =
        map
          (name: {
            users = [ name ];
            commands = [
              {
                command = "${vayumeRebuildScript}";
                options = [ "NOPASSWD" ];
              }
              {
                command = "${vayumeGcScript}";
                options = [ "NOPASSWD" ];
              }
            ];
          })
          (
            builtins.filter (name: builtins.elem "wheel" config.vayume.users.${name}.extraGroups) (
              builtins.attrNames config.vayume.users
            )
          );

      home-manager.users = lib.genAttrs (builtins.attrNames config.vayume.users) (
        name:
        {
          pkgs,
          lib,
          config,
          ...
        }:
        {
          imports = [
            inputs.dms.homeModules.dank-material-shell
            inputs.dms-plugin-registry.nixosModules.default
            inputs.danksession.homeManagerModules.default
          ];
          home.packages = [
            materialOSIcons
            pkgs.swayidle
          ];
          home.sessionVariables.QS_ICON_THEME = "MaterialOS";

          services.dankSession = {
            enable = true;
            package = inputs.danksession.packages.${pkgs.system}.default;
            autoStart = true;
          };

          systemd.user.services.vayume-idle-lock = {
            Unit = {
              Description = "Lock the screen after 10 minutes idle";
              After = [ "graphical-session.target" ];
              PartOf = [ "graphical-session.target" ];
            };
            Service = {
              ExecStart = "${pkgs.swayidle}/bin/swayidle -w timeout 600 '${config.programs.dank-material-shell.package}/bin/dms ipc call lock lock'";
              Restart = "on-failure";
            };
            Install.WantedBy = [ "graphical-session.target" ];
          };
          xdg.configFile = {
            "DankMaterialShell/settings.json".force = true;
            "DankMaterialShell/plugin_settings.json".force = true;

            "DankMaterialShell/plugins/NixMonitor/config.json" = {
              force = true;
              text = builtins.toJSON {
                generationsCommand = [
                  "sh"
                  "-c"
                  "ls -d /nix/var/nix/profiles/per-user/$(whoami)/home-manager-*-link 2>/dev/null | wc -l"
                ];
                storeSizeCommand = [
                  "sh"
                  "-c"
                  "du -sh /nix/store 2>/dev/null | cut -f1"
                ];
                rebuildCommand = [
                  "sh"
                  "-c"
                  "sudo ${vayumeRebuildScript} 2>&1"
                ];
                gcCommand = [
                  "sh"
                  "-c"
                  "sudo ${vayumeGcScript} 2>&1"
                ];
                updateInterval = 300;
              };
            };
          };
          home.activation.seedDmsSession =
            let
              defaultSession = pkgs.writeText "dms-default-session.json" (
                builtins.toJSON {
                  configVersion = 4;
                  wallpaperPath = "${./../assets/wallpapers}/wallhaven-w5xdzx.jpg";
                  wallpaperCyclingFolderPath = "${./../assets/wallpapers}";
                }
              );
            in
            lib.hm.dag.entryAfter [ "writeBoundary" ] ''
              sessionFile="$HOME/.local/state/DankMaterialShell/session.json"
              if [ ! -e "$sessionFile" ]; then
                run mkdir -p "$(dirname "$sessionFile")"
                run cp "${defaultSession}" "$sessionFile"
                run chmod u+w "$sessionFile"
              fi
            '';

          programs.dank-material-shell = {
            enable = true;
            package = lib.mkForce dmsShellPatched;

            systemd = {
              enable = true;
              restartIfChanged = true;
            };

            enableSystemMonitoring = true;
            enableDynamicTheming = true;
            enableAudioWavelength = true;
            plugins = {
              wallpaperCarousel = {
                enable = true;

                settings = {
                  enabled = true;
                  wallpaperDirectory = "${./../assets/wallpapers}";

                  borderWidth = 0;
                  itemHeight = 472;
                  selectedScale = 106;
                  expandMultiplier = 118;
                  cornerRadius = 3;
                  overlayOpacity = 84;
                };
              };

              dankAsusControlCenter = {
                enable = true;
                src = lib.mkForce dankAsusControlCenterPatched;
                settings = {
                  showBatteryIcon = false;
                  useThemeColors = true;
                };
              };

              dankQuickSearch = {
                enable = true;
                settings = {
                  trigger = "!";
                  defaultEngine = "duckduckgo";
                };
              };

              dankBitwarden = {
                enable = true;
                settings = {
                  trigger = "[";
                  noTrigger = false;
                  loginAction = "copy:password";
                  cardAction = "copy:number";
                  identityAction = "copy:name";
                  sshKeyAction = "copy:public_key";
                };
              };

              spotifyMatugen.enable = true;

              pureLyrics.enable = true;

              nixMonitor = {
                enable = true;
                src = lib.mkForce nixMonitorPatched;
                settings = {
                  showGenerations = true;
                  showStoreSize = true;
                  gcThresholdGB = 50;
                  checkUpdates = true;
                  nixpkgsChannel = "nixos-unstable";
                  updateCheckInterval = 3600;
                };
              };

              fullscreenPowerMenu.enable = true;

              cavaVisualizer = {
                enable = true;
                src = lib.mkForce cavaVisualizerPatched;
              };

              nixPackageRunner = {
                enable = true;
                settings = {

                  runSourceMode = "latest_unstable";
                };
              };

              screenshotPlus.enable = true;

              dankSession = {
                enable = true;
                src = lib.mkForce inputs.danksession.outPath;
              };
            };

            settings = {

              currentThemeName = "dynamic";
              currentThemeCategory = "dynamic";

              matugenScheme = "scheme-content";

              cornerRadius = 12;

              useAutoLocation = true;

              lockBeforeSuspend = true;

              matugenTemplateZenBrowser = false;
              matugenTemplateZed = false;

              blurEnabled = true;

              blurredWallpaperLayer = true;
              blurWallpaperOnOverview = true;

              controlCenterShowMicPercent = true;

              controlCenterWidgets = [
                {
                  id = "volumeSlider";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "brightnessSlider";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "wifi";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "bluetooth";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "audioOutput";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "audioInput";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "nightMode";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "darkMode";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "idleInhibitor";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "plugin_tor";
                  enabled = true;
                  width = 50;
                }
                {
                  id = "diskUsage";
                  enabled = true;
                  width = 50;
                  mountPath = "/";
                  showMountPath = true;
                }
              ];

              showWorkspaceIndex = true;

              appIdSubstitutions = [ ];

              filePickerUsageHistory = {
                code = {
                  count = 1;
                  lastUsed = 1787340048369;
                  name = "Visual Studio Code";
                };
              };

              appDrawerSectionViewModes = {
                apps = "list";
              };

              cursorSettings = {
                theme = "System Default";
                size = 24;

                niri = {
                  hideWhenTyping = false;
                  hideAfterInactiveMs = 0;
                };

                hyprland = {
                  hideOnKeyPress = true;
                  hideOnTouch = false;
                  inactiveTimeout = 0;
                };

                mango = {
                  cursorHideTimeout = 0;
                };
              };

              fontFamily = theme.font;
              monoFontFamily = theme.font;

              gtkThemingEnabled = true;
              qtThemingEnabled = true;

              terminalsAlwaysDark = true;

              showDock = true;
              dockAutoHide = true;

              dockPosition = 3;

              dockSpacing = 12;
              dockMargin = 10;

              dockBorderEnabled = true;
              dockBorderColor = "secondary";

              dockLauncherEnabled = true;

              osdPowerProfileEnabled = true;

              barConfigs = [
                {
                  id = "default";
                  name = "Main Bar";

                  enabled = true;
                  position = 0;

                  screenPreferences = [ "all" ];
                  showOnLastDisplay = true;

                  leftWidgets = [
                    "launcherButton"
                    "workspaceSwitcher"

                    {
                      id = "focusedWindow";
                      enabled = true;
                      focusedWindowSize = 1;
                      focusedWindowCompactMode = true;
                      focusedWindowShowIcon = true;
                    }
                  ];

                  centerWidgets = [
                    {
                      id = "music";
                      enabled = true;
                      mediaSize = 0;
                    }

                    {
                      id = "clock";
                      enabled = true;
                      clockCompactMode = false;
                    }

                    "weather"
                  ];

                  rightWidgets = [
                    {
                      id = "nixMonitor";
                      enabled = true;
                    }
                    "systemTray"
                    "clipboard"
                    "cpuUsage"
                    "memUsage"

                    "notificationButton"

                    {
                      id = "battery";
                      enabled = true;
                      showBatteryPercent = true;
                      showBatteryPercentOnlyOnBattery = false;
                      showBatteryTime = false;
                      batteryPillStyle = false;
                      batteryPillPercentSign = false;
                    }

                    {
                      id = "dankAsusControlCenter";
                      enabled = true;
                    }

                    {
                      id = "dankSession";
                      enabled = true;
                    }

                    "controlCenterButton"
                  ];

                  spacing = 4;
                  innerPadding = 3;

                  barInsetPadding = -1;
                  bottomGap = 0;

                  transparency = 0.50;
                  widgetTransparency = 1;

                  squareCorners = false;
                  noBackground = true;

                  maximizeWidgetIcons = false;
                  maximizeWidgetText = false;

                  removeWidgetPadding = false;
                  widgetPadding = 8;

                  gothCornersEnabled = false;
                  gothCornerRadiusOverride = false;
                  gothCornerRadiusValue = 12;

                  borderEnabled = false;
                  borderColor = "surfaceText";
                  borderOpacity = 1;
                  borderThickness = 1;

                  widgetOutlineEnabled = false;
                  widgetOutlineColor = "primary";
                  widgetOutlineOpacity = 1;
                  widgetOutlineThickness = 1;

                  fontScale = 1;
                  iconScale = 1;

                  autoHide = false;
                  autoHideStrict = false;
                  autoHideDelay = 250;

                  showOnWindowsOpen = false;
                  openOnOverview = false;

                  visible = true;

                  popupGapsAuto = true;
                  popupGapsManual = 4;

                  maximizeDetection = true;

                  useOverlayLayer = false;

                  scrollEnabled = true;
                  scrollXBehavior = "column";
                  scrollYBehavior = "workspace";

                  shadowIntensity = 0;
                  shadowOpacity = 60;
                  shadowColorMode = "default";
                  shadowCustomColor = "#000000";

                  clickThrough = false;

                  hoverPopouts = true;
                  hoverPopoutDelay = 150;
                }
              ];

              desktopClockCustomColor = {
                r = 1;
                g = 1;
                b = 1;
                a = 1;

                hsvHue = -1;
                hsvSaturation = 0;
                hsvValue = 1;

                hslHue = -1;
                hslSaturation = 0;
                hslLightness = 1;

                valid = true;
              };

              systemMonitorCustomColor = {
                r = 1;
                g = 1;
                b = 1;
                a = 1;

                hsvHue = -1;
                hsvSaturation = 0;
                hsvValue = 1;

                hslHue = -1;
                hslSaturation = 0;
                hslLightness = 1;

                valid = true;
              };

              desktopWidgetInstances = [
                {
                  id = "dw_1788083196111_hpk1tmarl";
                  widgetType = "pureLyrics";
                  name = "Pure Lyrics";
                  enabled = true;

                  config = {
                    displayPreferences = [ "all" ];
                    showOnOverlay = false;
                    showOnOverview = false;
                    clickThrough = true;
                    borderOpacity = 0;
                    backgroundOpacity = 0;
                    colorMode = "primary";
                    lineCount = "5";
                    fontSize = 35;
                    textAlign = "center";
                    showOnOverviewOnly = false;
                    syncPositionAcrossScreens = true;
                  };

                  positions._synced = {
                    x = 0;
                    width = 9999;
                    height = 253;
                  };
                }

                {
                  id = "dw_1789200000000_cavaviz01";
                  widgetType = "cavaVisualizer";
                  name = "Cava Visualizer";
                  enabled = true;

                  config = {
                    vizMode = "curve-outline";
                    curvePoints = 24;
                    curveLineWidth = 3;
                    barCount = 20;
                    barSpacing = 4;
                    barWidth = 0;
                    orientation = "bottom";
                    sensitivity = 100;
                    channels = "mono";
                    colorChoice = "primary";
                    silenceTimeout = 5;
                    bgOpacity = 0;
                    opacity = 100;
                    syncPositionAcrossScreens = true;
                  };
                  positions._synced = {
                    x = 0;
                    y = 0.9;
                    width = 9999;
                    height = 120;
                  };
                }
              ];

              builtInPluginSettings = {
                dms_settings_search = {
                  trigger = "?";
                };

                dms_clipboard_search = {
                  trigger = "cb";
                };
              };

              clipboardClickToPaste = true;
              frameEnabled = true;
              frameOpacity = 0.45;
              configVersion = 13;
            };
          };
        }
      );
    };
}
